# Export the Qt version used by rviz.
set(rviz_QT_VERSION @rviz_QT_VERSION@)

# Set the rviz_DEFAULT_PLUGIN_LIBRARIES variable.
# If the target exists, then this file was included during a cmake invocation
# that is shared with rviz's cmake, e.g. when building with catkin_make.
if(TARGET @rviz_DEFAULT_PLUGIN_LIBRARY_TARGET_NAME@)
  # So just set it to the target name.
  set(rviz_DEFAULT_PLUGIN_LIBRARIES @rviz_DEFAULT_PLUGIN_LIBRARY_TARGET_NAME@)
else()
  # Otherwise rviz was built separately and this was sourced from a devel space or install space.
  # If the location file doesn't exist at this point it is an error.
  if(EXISTS "${rviz_DIR}/default_plugin_location.cmake")
    # The file being included is generated by src/rviz/default_plugin/CMakeLists.txt
    include("${rviz_DIR}/default_plugin_location.cmake")
    set(rviz_DEFAULT_PLUGIN_LIBRARIES
      "${rviz_DIR}/../../../@CATKIN_PACKAGE_LIB_DESTINATION@/${rviz_DEFAULT_PLUGIN_FILE_NAME}")
  else()
    message(FATAL_ERROR "the default_plugin_location.cmake file was not found and is required")
  endif()
endif()
